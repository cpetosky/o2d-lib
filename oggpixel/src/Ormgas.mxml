<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
				width="100%"
				height="100%"
				horizontalAlign="center"
				verticalAlign="top"
				verticalGap="20"
				creationComplete="init()">
	
	<mx:Script>
		<![CDATA[
			import com.automatastudios.audio.audiodecoder.AudioDecoder;
			import com.automatastudios.audio.audiodecoder.decoders.*;
			
			import flash.events.Event;
			import flash.events.IOErrorEvent;
			import flash.net.URLStream;
			import flash.net.URLRequest;
			
			private const BUFFER_SIZE:uint = 8192;
			private const FILENAME:String = "http://stream.rainwave.cc:8000/stream.ogg";
			
			public var decoder:AudioDecoder;
			public var stream:URLStream;
			public var channel:SoundChannel;		

			public function init():void {
				currentState = "startup";

				decoder = new AudioDecoder();
				decoder.addEventListener(Event.INIT, onDecoderInit);
				decoder.addEventListener(Event.COMPLETE, onDecoderComplete);
				decoder.addEventListener(IOErrorEvent.IO_ERROR, onDecoderIOError);
			}
			
			public function playSound(fileName:String, decoderClass:Class):void {
				currentState = "playing";
				if (stream != null) {
					stream.close();
					channel.stop();
				}
				
				taInfo.text = ":: Loading: " + fileName + "\n";
	
				stream = new URLStream();		
				decoder.load(stream, decoderClass, BUFFER_SIZE);
				stream.load(new URLRequest(fileName));
			}
			
			private function onDecoderInit(event:Event):void {
				channel = decoder.play();
				taInfo.text += decoder.audioInfo.toString();
			}
			
			private function onDecoderComplete(event:Event):void {
				taInfo.text += ":: Sound completed!";	
			}
			
			private function onDecoderIOError(event:IOErrorEvent):void {
				taInfo.text = ":: Error: " + event.text;
			}
		]]>
	</mx:Script>
	<mx:states>
		<mx:State name="startup">
			<mx:AddChild>
				<mx:VBox width="100%">
					<mx:HBox width="100%">
						<mx:Label text="Listener ID:"/>
						<mx:TextInput id="listenerID" text="{SharedObject.getLocal('rainwave').data['id']}"/>
						<mx:Button label="Listen!">
							<mx:click><![CDATA[
								if (listenerID.text.charAt(0) != "?")
									listenerID.text = "?" + listenerID.text;
								SharedObject.getLocal("rainwave").data["id"] = listenerID.text;
								playSound(FILENAME + listenerID.text, OggVorbisDecoder);
							]]></mx:click>
						</mx:Button>
					</mx:HBox>
					<mx:Text width="100%">
						<mx:text><![CDATA[
Open up your rainwave playlist file in text editor, your listener ID is the bit after the ?.
Example: http://stream.rainwave.cc:8000/stream.ogg?198:123fcd481bbb		
Your listener ID would be: 198:123fcd481bbb		
						]]></mx:text>
					</mx:Text>
					
				</mx:VBox>
			</mx:AddChild>
		</mx:State>
		<mx:State name="playing">
			<mx:AddChild>
				<mx:Panel width="100%" title="File Information">
					<mx:TextArea id="taInfo" width="100%" height="200" />
				</mx:Panel>
			</mx:AddChild>
		</mx:State>
	</mx:states>	
	
</mx:Application>
